# -*- mode: bazel; -*-
# vi: set ft=bazel:

#
# BUILD.bazel
# RVO2-3D Library
#
# SPDX-FileCopyrightText: 2008 University of North Carolina at Chapel Hill
# SPDX-License-Identifier: Apache-2.0
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Please send all bug reports to <geom@cs.unc.edu>.
#
# The authors may be contacted via:
#
# Jur van den Berg, Stephen J. Guy, Jamie Snape, Ming C. Lin, Dinesh Manocha
# Dept. of Computer Science
# 201 S. Columbia St.
# Frederick P. Brooks, Jr. Computer Science Bldg.
# Chapel Hill, N.C. 27599-3175
# United States of America
#
# <https://gamma.cs.unc.edu/RVO2/>
#

load("@rules_cc//cc:defs.bzl", "cc_binary")
load("@rules_license//rules:license.bzl", "license")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files")
load("@rules_pkg//pkg:pkg.bzl", "pkg_deb", "pkg_tar")

package(default_applicable_licenses = [":license"])

licenses(["notice"])  # Apache-2.0

license(
    name = "license",
    copyright_notice = "2008 University of North Carolina at Chapel Hill",
    license_kinds = ["@rules_license//licenses/spdx:Apache-2.0"],
    license_text = "LICENSE",
    visibility = ["//visibility:public"],
)

exports_files(
    ["LICENSE"],
    visibility = ["//visibility:public"],
)

pkg_files(
    name = "doc",
    srcs = ["LICENSE"],
    attributes = pkg_attributes(mode = "0644"),
    prefix = "usr/share/doc/RVO3D",
    visibility = ["//visibility:private"],
)

cc_binary(
    name = "libRVO3D.so",
    linkshared = True,
    visibility = ["//visibility:private"],
    deps = ["//src:RVO3D"],
)

pkg_files(
    name = "lib",
    srcs = [":libRVO3D.so"],
    attributes = pkg_attributes(mode = "0644"),
    prefix = "usr/lib",
    visibility = ["//visibility:private"],
)

# REUSE-IgnoreStart
genrule(
    name = "pc",
    outs = ["RVO3D.pc"],
    cmd = """
cat << 'EOF' > $@
#
# RVO3D.pc
# RVO2-3D Library
#
# SPDX-FileCopyrightText: 2008 University of North Carolina at Chapel Hill
# SPDX-License-Identifier: Apache-2.0
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Please send all bug reports to <geom@cs.unc.edu>.
#
# The authors may be contacted via:
#
# Jur van den Berg, Stephen J. Guy, Jamie Snape, Ming C. Lin, Dinesh Manocha
# Dept. of Computer Science
# 201 S. Columbia St.
# Frederick P. Brooks, Jr. Computer Science Bldg.
# Chapel Hill, N.C. 27599-3175
# United States of America
#
# <https://gamma.cs.unc.edu/RVO2/>
#

prefix=/usr
exec_prefix=$${prefix}
libdir=$${exec_prefix}/lib
includedir=$${prefix}/include/RVO3D

Name: RVO2-3D Library
Description: Optimal Reciprocal Collision Avoidance in Three Dimensions
URL: https://gamma.cs.unc.edu/RVO2/
Version: 1.1.0
Libs: -L$${libdir} -lRVO3D
Cflags: -I$${includedir}
EOF
""",
    visibility = ["//visibility:private"],
)
# REUSE-IgnoreEnd

pkg_files(
    name = "pkgconfig",
    srcs = ["RVO3D.pc"],
    attributes = pkg_attributes(mode = "0644"),
    prefix = "usr/lib/pkgconfig",
    visibility = ["//visibility:private"],
)

pkg_tar(
    name = "RVO3D",
    srcs = [
        ":doc",
        ":lib",
        ":pkgconfig",
        "//src:include",
    ],
    extension = "tar.gz",
    visibility = ["//visibility:private"],
)

pkg_deb(
    name = "deb",
    architecture = "amd64",
    data = ":RVO3D",
    depends = [
        "libc6",
        "libgcc-s1",
        "libstdc++6",
    ],
    description = "Optimal Reciprocal Collision Avoidance in Three Dimensions",
    homepage = "https://gamma.cs.unc.edu/RVO2/",
    maintainer = "Jamie Snape",
    package = "rvo3d",
    priority = "optional",
    section = "libdevel",
    version = "1.1.0",
    visibility = ["//visibility:private"],
)
