# -*- mode: bazel; -*-
# vi: set ft=bazel:

#
# BUILD.bazel
# RVO2-3D Library
#
# SPDX-FileCopyrightText: 2008 University of North Carolina at Chapel Hill
# SPDX-License-Identifier: Apache-2.0
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Please send all bug reports to <geom@cs.unc.edu>.
#
# The authors may be contacted via:
#
# Jur van den Berg, Stephen J. Guy, Jamie Snape, Ming C. Lin, Dinesh Manocha
# Dept. of Computer Science
# 201 S. Columbia St.
# Frederick P. Brooks, Jr. Computer Science Bldg.
# Chapel Hill, N.C. 27599-3175
# United States of America
#
# <https://gamma.cs.unc.edu/RVO2/>
#

load("@rules_cc//cc:defs.bzl", "cc_shared_library")
load("@rules_license//rules:license.bzl", "license")
load("@rules_license//rules:package_info.bzl", "package_info")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files")
load("@rules_pkg//pkg:pkg.bzl", "pkg_deb", "pkg_tar")

package(default_package_metadata = [
    ":license",
    ":package_info",
])

license(
    name = "license",
    copyright_notice = "2008 University of North Carolina at Chapel Hill",
    license_kinds = ["@rules_license//licenses/spdx:Apache-2.0"],
    license_text = "LICENSE",
    visibility = ["//:__subpackages__"],
)

package_info(
    name = "package_info",
    package_name = "RVO2-3D Library",
    package_url = "https://gamma.cs.unc.edu/RVO2/",
    package_version = "1.1.0",
    visibility = ["//:__subpackages__"],
)

exports_files(
    ["LICENSE"],
    visibility = ["//visibility:public"],
)

pkg_files(
    name = "doc",
    srcs = ["LICENSE"],
    attributes = pkg_attributes(mode = "0644"),
    prefix = "usr/share/doc/RVO3D",
)

cc_shared_library(
    name = "RVO3D",
    deps = ["//src:RVO3D"],
)

pkg_files(
    name = "lib",
    srcs = [":RVO3D"],
    attributes = pkg_attributes(mode = "0644"),
    prefix = "usr/lib",
)

genrule(
    name = "rvo3d_cps",
    outs = ["RVO3D.cps"],
    cmd = """
cat << 'EOF' > $@
{
  "compat_version": "1",
  "components":
  {
    "RVO3D":
    {
      "includes": ["@prefix@/include/RVO3D"],
      "type": "dylib"
    }
  },
  "cps_path": "@prefix@/lib/cps/RVO3D",
  "cps_version": "0.13.0",
  "default_components": ["RVO3D"],
  "description": "Optimal Reciprocal Collision Avoidance in Three Dimensions",
  "license": "Apache-2.0",
  "name": "RVO3D",
  "version": "1.1.0",
  "version_schema": "simple",
  "website": "https://gamma.cs.unc.edu/RVO2/"
}
EOF
""",
)

pkg_files(
    name = "cps",
    srcs = ["RVO3D.cps"],
    attributes = pkg_attributes(mode = "0644"),
    prefix = "usr/lib/cps/RVO3D",
)

# REUSE-IgnoreStart
genrule(
    name = "rvo3d_pc",
    outs = ["RVO3D.pc"],
    cmd = """
cat << 'EOF' > $@
#
# RVO3D.pc
# RVO2-3D Library
#
# SPDX-FileCopyrightText: 2008 University of North Carolina at Chapel Hill
# SPDX-License-Identifier: Apache-2.0
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Please send all bug reports to <geom@cs.unc.edu>.
#
# The authors may be contacted via:
#
# Jur van den Berg, Stephen J. Guy, Jamie Snape, Ming C. Lin, Dinesh Manocha
# Dept. of Computer Science
# 201 S. Columbia St.
# Frederick P. Brooks, Jr. Computer Science Bldg.
# Chapel Hill, N.C. 27599-3175
# United States of America
#
# <https://gamma.cs.unc.edu/RVO2/>
#

prefix=/usr
exec_prefix=$${prefix}
libdir=$${exec_prefix}/lib
includedir=$${prefix}/include/RVO3D

Name: RVO2-3D Library
Description: Optimal Reciprocal Collision Avoidance in Three Dimensions
URL: https://gamma.cs.unc.edu/RVO2/
Version: 1.1.0
Libs: -L$${libdir} -lRVO3D
Cflags: -I$${includedir}
EOF
""",
)
# REUSE-IgnoreEnd

pkg_files(
    name = "pkgconfig",
    srcs = ["RVO3D.pc"],
    attributes = pkg_attributes(mode = "0644"),
    prefix = "usr/lib/pkgconfig",
)

pkg_tar(
    name = "tar",
    srcs = [
        ":cps",
        ":doc",
        ":lib",
        ":pkgconfig",
        "//src:include",
    ],
    extension = "tar.gz",
)

pkg_deb(
    name = "deb",
    architecture = select({
        "@platforms//cpu:arm": "amd64",
        "@platforms//os:macos": "arm64",
        "//conditions:default": "all",
    }),
    data = ":tar",
    depends = [
        "libc6",
        "libgcc-s1",
        "libstdc++6",
    ],
    description = "Optimal Reciprocal Collision Avoidance in Three Dimensions",
    homepage = "https://gamma.cs.unc.edu/RVO2/",
    license = "Apache-2",
    maintainer = "Jamie Snape",
    package = "rvo3d",
    priority = "optional",
    section = "contrib/libdevel",
    triggers = "triggers",
    version = "1.1.0",
)
